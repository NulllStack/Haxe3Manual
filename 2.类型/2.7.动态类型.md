# 2.7.动态类型

Haxe 虽然是静态类型系统，但是可以通过使用 `Dynamic` 类型使得在本质上关闭类型检查，一个 **dynamic 值** 可以被赋值到任意类型上，而任意类型也可以被赋值给它。但是会有以下缺点：

*While Haxe has a static type system, it can essentially be disabled by using the `Dynamic` type. A **dynamic value** can be assigned to anything and anything can be assigned to it. This has several drawbacks:*

- 编译器不再对赋值操作、函数调用或任何具有预期类型的构造上执行类型检查，
- 一些如编译至静态目标时的优化不再可用。
- 一些常见的错误，比如字段访问的拼写错误导致的错误将无法在编译时被捕获，并可能导致错误在运行时被触发。
- 如果字段是通过 Dynamic 类型被使用，[无用代码消除（第8.2节）](/8.编译器功能/8.2.无用代码消除)无法侦测到此类字段是否被使用过。

- *The compiler can no longer type-check assignments, function calls and other constructs where specific types are expected.*
- *Certain optimizations, in particular when compiling to static targets, can no longer be employed.*
- *Some common errors such as typos in field accesses cannot be caught at compile-time and likely cause errors at runtime.*
- *[Dead Code Elimination](https://haxe.org/manual/cr-dce.html) cannot detect used fields if they are used through `Dynamic`.*

使用 Dynamic 类型可能引发运行时错误的例子非常容易出现。考虑以下的两行代码在编译至静态目标平台的情况：

*It is very easy to come up with examples where the usage of `Dynamic` can cause problems at runtime. Consider compiling the following two lines to a static target:*

```haxe
var d:Dynamic = 1; 
d.foo;
```

尝试在 Flash 播放器运行编译后的程序，会产生一个错误 ，`Property foo not found on Number and there is no default value` 。而如果不使用 `Dynamic` 的话，这个错误会在编译时被侦测到。

*Trying to run a compiled program in the Flash Player yields an error `Property foo not found on Number and there is no default value`. Without `Dynamic`, this would have been detected at compile-time.*

应该尽量少的使用 `Dynamic` ，因为通常情况下总会有更好的手段，然而，它有时候也是一个实用的解决方案。比如 Haxe 的 [Reflection（反射）](/10.标准库/10.7.反射) API 就使用了它。另外，`Dynamic` 类型用来处理编译时（compile-time）无法得知的自定义数据结构的时候是一个最好的选择。

*Use of `Dynamic` should be minimized as there are often better options available. However, it is occasionally the practical solution; parts of the Haxe [Reflection](https://haxe.org/manual/std-reflection.html) API make use of it. Additionally, using `Dynamic` can be the best choice to handle custom data structures that are not known at compile-time.*

`Dynamic` 在与一个 [monomorph（单变体）](/2.类型/2.9.单变体) 进行[一致性检查](/3.类型系统/3.5.一致性检查)时会有特殊的行为。单变体永远不会绑定为 `Dynamic` 类型，因为这么做会产生意料之外的结果，比如下面这样：

*`Dynamic` behaves in a special way when being [unified](https://haxe.org/manual/type-system-unification.html) with a [monomorph](https://haxe.org/manual/types-monomorph.html). Monomorphs are never bound to `Dynamic` which can have surprising results in examples such as this:*

```haxe
class Main {
  static function main() {
    var jsonData = '[1, 2, 3]';
    var json = haxe.Json.parse(jsonData);
    $type(json); // Unknown<0>
    for (i in 0...json.length) {
      // Array access is not allowed on
      // {+ length : Int }
      trace(json[i]);
    }
  }
}
```

尽管 `Json.parse` 的返回值类型为 `Dynamic` ，局部变量 `json` 并不会绑定为该类型并保留为一个单变体。它随后在 `json.length` 字段访问表达式中被推断为一个 [匿名结构](/2.类型/2.5.匿名结构) ，因此在随后的 `json[0]` 数组下标访问时触发了错误。为了避免这个问题，变量 `json` 需要通过 `var json:Dynamic` 显式声明为 `Dynamic` 类型。

*Although the return type of `Json.parse` is `Dynamic`, the type of local variable `json` is not bound to it and remains a monomorph. It is then inferred as an [anonymous structure](https://haxe.org/manual/types-anonymous-structure.html) upon the `json.length` field access, which causes the following `json[0]` array access to fail. In order to avoid this, the variable `json`can be explicitly typed as `Dynamic` by using `var json:Dynamic`.*



> **花絮：Haxe 3 之前的 Dynamic 类型推断**
>
> Haxe 3 编译器不会自动推断出 `Dynamic` 类型，所以用户必须显式地声明它的类型。在以前的版本中 Haxe 会自动推断混合数组 如 `[1, true, "foo"]` 的类型为 `Array<Dynamic>` 。但我们随后发现此类行为引发了太多的类型问题，并最终在 Haxe 3 中移除了这个功能。
>
> ##### *[Trivia: Dynamic Inference before Haxe 3](https://haxe.org/manual/types-dynamic.html#trivia-dynamic-inference-before-haxe-3)*
>
> *The Haxe 3 compiler never infers a type to `Dynamic`, so users must be explicit about it. Previous Haxe versions used to infer arrays of mixed types, e.g. `[1, true, "foo"]`, as `Array<Dynamic>`. We found that this behavior introduced too many type problems and thus removed it for Haxe 3.*
>
> 
>
> **花絮：标准库中的 Dynamic**
>
> 在 Haxe 3 之前的标准库中 Dynamic 类型的使用颇为频繁。但随着 Haxe 类型系统的持续改进，在 Haxe 3 发布前 Dynamic 的出现逐步降低。
>
> ##### *[Trivia: Dynamic in the Standard Library](https://haxe.org/manual/types-dynamic.html#trivia-dynamic-in-the-standard-library)*
>
> *Dynamic was quite frequent in the Haxe Standard Library before Haxe 3. With the continuous improvements of the Haxe type system, the occurrences of Dynamic were reduced over the releases leading to Haxe 3.*



# 2.7.1.Dynamic使用类型参数

Dynamic 是一个特殊的类型，因为它允许使用和不使用一个 [类型参数（第3.2节）](http:///#) 来进行显式的声明。如果这样一个类型参数被提供，[Dynamic（第2.7节）](http:///#) 中描述的
 语义被限制为所有的字段兼容该参数类型：

```haxe
var att : Dynamic<String> = xml.attributes;
// 有效, 值为一个 String 类型 
att.name = "Nicolas"; 
// dito (这个文档太旧了) 
att.age = "26"; 
// error,值不是 String 类型
att.income = 0;
```



# ~~2.7.2.实现Dynamic （此小节已过时）~~

类可以 [实现（第2.3.3节）](http:///#) Dynamic 类型，和提供任意字段访问的 Dynamic 。前一种情况，字段可以有任何类型，而后一种，它们被限制兼容参数类型：

```haxe
 class ImplementsDynamic 
    implements Dynamic<String> { 
        public var present:Int; 
        public function new() {} 
}
class Main {
    static public function main() {
        var c = new ImplementsDynamic(); 
        // 有效的，present 是一个存在的字段 
        c.present = 1; 
        // 有效，分配的值是一个 String 
        c.stringField = "foo"; 
        // 错误，Int应该是 String 
        //c.intField = 1;
    }
} 
```

实现 Dynamic 不符合实现其它接口的需求。预期的字段仍然必须被明确实现。实现 Dynamic 的类（带或者不带类型参数）也可以利用一个特别的方法名字叫做 `resolve` 。如果一个[ 读访问（第4.2节）](http:///#) 被做出，而且被讨论的字段不存在，`resolve` 方法被调用，并以这个字段的名字作为参数：

```haxe
class Resolve implements Dynamic<String> { 
    public var present:Int;
    public function new() {} 
    function resolve(field:String) { 
        return "Tried to resolve " +field; 
    }
}

class Main { 
    static public function main() { 
        var c = new Resolve();
        c.present = 2; 
        trace(c.present); 
        trace(c.resolveMe);
    }
}
```

