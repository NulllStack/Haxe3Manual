# 2.5.匿名结构

匿名结构可以用来组织数据，而不用显式地创建一个类型。下面的示例创建了一个结构，包含两个字段 `x` 和 `name`，并分别初始化它们的值为 `12` 和 “`foo`”：

```haxe
class Main { 
    static public function main() { 
        var myStructure = { x: 12, name: "foo"};
    } 
} 
```

一般语法规则如下：

- 一个包括在花括号 `{}` 内的结构
- 一个使用逗号 `,` 分隔的键值对列表
- 冒号 `:` 用来分隔的键和值，键名必须是一个有效的 [标识符（第5章）](http:///#)
- 值可以是任何 Haxe 表达式

规则 4 意味着结构可以被嵌套和混合，如：

```haxe
var user = {
    name : "Nicolas",
    age : 32, 
    pos : [//这是一个数组
        { x : 0, y : 0 }, //这是一个匿名结构
        { x : 1, y : -1 }//这也是一个匿名结构
    ],
}; 
```

匿名结构的字段像类一样可以通过点号操作符 `.` 访问，如下：

```haxe
// 获取键 name 的值，即 "Nicolas" 
user.name; 
// 设置 键 age 的值为 33 
user.age = 33; 
```

值得注意的是，使用匿名结构不会破坏类型系统。编译器确保只能访问可用的字段，也就是说，如下的程序不会编译：

```haxe
class Test { 
    static public function main() {
        var point = { x: 0.0, y: 12.0 }; 
        // { y : Float, x : Float } 没有字段 z 
        point.z; 
    } 
}
```

错误信息表明编译器了解 `point` 的类型：它是一个包含了两个 `Float` 类型 `x` 和 `y` 字段的匿名结构。由于它没有字段 `z`，访问失败。 `point` 的类型通过 [类型推断（第3.6节）](http:///#) 被得知，这使得我们不需要为局部变量使用显式的类型声明。但是，如果 `point` 是一个字段，那么它必须通过如下形式进行声明：

```haxe
class Path { 
    var start : { x : Int, y : Int };
    var target : { x : Int, y : Int };
    var current : { x : Int, y : Int }; 
} 
```

为了避免这种冗余的类型声明形式，尤其是对于更复杂的结构，建议使用一个 [typedef（第3.1节）](http:///#) 关键字：

```haxe
typedef Point = { x : Int, y : Int } 

class Path { 
    var start : Point; 
    var target : Point; 
    var current : Point; 
}
```

