# 6.2.Externs

使用 extern 关键字可以以一种类型安全的方式对目标语言特定的交互进行描述，使用他们定义的类与通常的类定义有以下不同：

> Externs can be used to describe target-speciﬁc interaction in a type-safe manner. They are deﬁned like normal classes, except that

-  class 关键字前通过 extern 关键字进行修饰，
- 其中的方法没有具体的表达式（或者说没有具体实现）且，
- 所有的参数类型与返回类型都必须显式地声明。

> - the class keyword is preceded by the extern keyword,
> - methods (4.3) have no expressions and
> - all argument and return types are explicit.

比如 Haxe标准库（第10章）中的一个例子，Math 类，摘录如下：

> A common example from the Haxe Standard Library (10) is the Math class, as an excerpt shows:

```haxe
extern class Math 
{  
    static var PI(default,null) : Float; 
    static function floor(v:Float):Int;
}
```

我们看到，externs 既可以定义方法也可以定义变量（实际上，PI是声明为一个只读属性（第4.2节））。一旦编译器可以使用这个信息，则会相应的允许对字段进行访问并获知其类型：

> We see that externs can deﬁne both methods and variables (actually, PI is declared as a readonly property (4.2)). Once this information is available to the compiler, it allows ﬁeld access accordingly and also knows the types:

```haxe
class Main { 
    static public function main() { 
        var pi = Math.floor(Math.PI); 
        $type(pi); // Int 
    } 
} 
```

这段代码能够运行，因为 floor 方法的返回类型被声明为 Int。

> This works because the return type of method floor is declared to be Int.

Haxe标准库中有许多 Flash 与 JavaScript 的 extern。它们使得能够以类型安全的方式访问原生的 API 并以此为基础设计高层 API。Extern 也被 haxelib 中的许多流行的原生库所使用。

> The Haxe Standard Library comes with many externs for the Flash and JavaScript target. They allow accessing the native APIs in a type-safe manner and are instrumental for designing higher-level APIs. There are also externs for many popular native libraries on haxelib (11).

Flash，Java和C#目标语言允许通过命令行（第7章）直接包括原生的库。目标特定的细节在每个目标语言细节（第12章）等各自的章节。

> The Flash, Java and C# targets allow direct inclusion of native libraries from command line (7). Target-speciﬁc details are explained in the respective sections of Target Details (Chapter 12).

一些目标语言如Python或者JavaScript可能需要产生附加的 import 代码，为原生模块加载一个extern 类。Haxe提供声明这样的依赖关系在各自的目标语言向细节（第12章）的方式。。

> Some targets such as Python or JavaScript may require generating additional ”import” code that loads an extern class from a native module. Haxe provides ways to declare such dependencies also described in respective sections Target Details (Chapter 12).

**Haxe3.2.0以后剩余的参数和类型选择**

> Rest arguments and type choices Since Haxe 3.2.0

haxe.extern 包提供两种类型帮助映射原生的语义到Haxe：

> The haxe.extern package provides two types that help mapping native semantics to Haxe:

**Rest** 这个类型可以被实用为一个最终的函数参数，来使传递一个额外的调用参数任意的数值到函数。类型参数可以被使用来限制这些参数为某个特定类型。

> **Rest**: This type can be used as a ﬁnal function argument to allow passing an arbitrary number of additional call arguments. The type parameter can be used to constrain these arguments to a speciﬁc type.

**EitherType**：这个类型允许使用任何一个类型参数类型，因此表示了一个类型选择。它可以被嵌套来允许更多的类型。

> **EitherType**: This type allows using either of its parameter types,thus representing a type choice. It can be nested to allow more than two different types.

我们在下面代码示例中演示了其用法：

> We demonstrate the usage in this code sample:

```haxe
import haxe.extern.Rest; 
import haxe.extern.EitherType; 

extern class MyExtern { 
    static function f1(s:String, r:Rest<Int>):Void; 
    static function f2(e:EitherType<Int, String>):Void;
} 

class Main { 
    static function main() { 
        MyExtern.f1("foo", 1, 2, 3); // use 1, 2, 3 as rest argument 
        MyExtern.f1("foo"); // no rest argument 
        //MyExtern.f1("foo", "bar"); // String should be Int 
        
        MyExtern.f2("foo"); 
        MyExtern.f2(12); 
        //MyExtern.f2(true); // Bool should be EitherType<Int, String> 
    } 
}
```



# 6.2.1.Native Metadata

When creating externs it is possible that certain fields will conflict with Haxe keywords, even though they are valid identifiers for the given target. These problems can be resolved by marking the conflicting fields with the metadata `:native`, with the intended identifier provided as a parameter.

```haxe
extern class A {
  public function new();
  @:native("final") var final_:Int;
}

class Main {
  public static function main():Void {
    var a = new A();
    a.final_ = 3;
  }
}
```

The generated JavaScript output shows that we are actually assigning to the `final` field:

```haxe
(function ($global) { "use strict";
var Test = function() { };
Test.main = function() {
    new A().final = 3;
};
Test.main();
})({});
```

This metadata can be applied to class and interface fields, but not the fields of a structure type.



# 6.2.2.Implementing Dynamic

Extern 类可以通过实现（implement） `Dynamic` 或 `Dynamic<T>` 使得允许对任意字段的访问。前者允许访问的字段可以是任意类型，后者则使得字段的类型被类型参数兼容的类型所约束。

Extern classes can [implement](https://haxe.org/manual/types-interfaces.html) `Dynamic` and `Dynamic<T>` which enables arbitrary field access. In the former case, fields can have any type, in the latter, they are constrained to be compatible with the parameter type:

```haxe
extern class ImplementsDynamic implements Dynamic<String> {
  public var present:Int;
  public function new();
}

class Main {
  static public function main() {
    var c = new ImplementsDynamic();
    // valid, present is an existing field
    c.present = 1;
    // valid, assigned value is a String
    c.stringField = "foo";
    // error, Int should be String
    // c.intField = 1;
  }
}
```

实现 `Dynamic` 并不会使得你所实现的其他接口所需的字段条件被被满足，你仍需显式地声明所需字段。

Implementing `Dynamic` does not satisfy the requirements of other implemented interfaces. The expected fields still have to be implemented explicitly.

> 花絮：非 extern 类实现 Dynamic
>
> 自 Haxe 4 开始，只有 extern 类才可以实现 Dynamic 。在之前的版本中，任何类都可以实现 Dynamic 并使得可以对该类的实例进行任意字段的读写行为，此外还有一个特殊的 `resolve` 函数用于对类中不存在的字段进行解析。现在，可以通过对 abstract 类的字段访问修饰符进行重写来实现类似特性。

> ##### [Trivia: Implementing `Dynamic` on non-externs](https://haxe.org/manual/types-dynamic-implemented.html#trivia-implementing-dynamic-on-non-externs)
>
> Starting with Haxe 4, implementing `Dynamic` is only allowed on extern classes. In previous versions any class could implement `Dynamic`, allowing arbitrary fields to be read or written. Additionally a special `resolve` method could be used to resolve read access to a non-existend field. A similar feature is now available as field access [operator overload](https://haxe.org/manual/types-abstract-operator-overloading.html) on abstracts.
